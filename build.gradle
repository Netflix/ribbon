// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories { mavenCentral() }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { mavenCentral() }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
    group = "com.netflix.${githubProjectName}" // TEMPLATE: Set to organization of project

    dependencies {
        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'com.netflix.servo:servo-core:0.4.32'
        compile 'com.google.guava:guava:11.0.2'
        compile 'com.netflix.archaius:archaius-core:0.5.3'
        compile 'com.netflix.netflix-commons:netflix-commons-util:0.1.0'
        compile 'javax.ws.rs:jsr311-api:1.1.1' 
        compile 'commons-collections:commons-collections:3.2.1'
        testCompile 'junit:junit:4.10'
        testCompile 'log4j:log4j:1.2.16'
    }   
}

project(':ribbon-core') {
    dependencies {
        compile 'com.netflix.netflix-commons:netflix-statistics:0.1.0'
    }
}

project(':ribbon-httpclient') {
    dependencies {
        compile project(':ribbon-core')
        compile 'org.apache.httpcomponents:httpclient:4.1.2' 
        compile 'com.sun.jersey:jersey-core:1.9.1'
        compile 'com.sun.jersey:jersey-client:1.9.1'        
        compile 'com.sun.jersey.contribs:jersey-apache-client4:1.8'
        testCompile 'asm:asm-all:3.2'
        testCompile 'commons-io:commons-io:2.0.1'
        testCompile 'com.sun.jersey:jersey-server:1.9.1'        
    }
}

project(':ribbon-eureka') {   
    dependencies {
        compile project(':ribbon-core')
        compile 'com.netflix.eureka:eureka-core:1.1.37' 
    }    
}
